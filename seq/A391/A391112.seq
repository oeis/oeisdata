%I A391112 #7 Dec 15 2025 16:53:42
%S A391112 0,0,0,0,0,0,0,1,1,0,0,0,0,0,0,1,0,2,0,0,1,0,0,2,2,0,2,0,0,1,0,3,0,1,
%T A391112 1,2,0,0,0,2,0,1,0,0,3,0,0,3,3,5,0,1,0,5,0,2,0,0,0,2,0,0,2,4,1,1,0,1,
%U A391112 0,2,0,6,0,0,4,0,1,1,0,3,6,0,0,2,0,1,0,2
%N A391112 a(n) is the number of Heronian triangles with semiperimeter n that can be split into two Pythagorean triangles.
%C A391112 Heronian triangles with odd perimeter cannot be split into two Pythagorean triangles. Reason: A Pythagorean triangle has perimeter i*(m^2 - k^2 + 2*m*k + m^2 + k^2) = 2*i*m*(m + k), hence even, with 0 or 2 odd sides. Since the two Pythagorean triangles share a leg, the resulting Heronian triangle has 0, 2, or 4 odd sides, so its perimeter is even.
%H A391112 Felix Huber, <a href="/A391112/b391112.txt">Table of n, a(n) for n = 1..10000</a>
%H A391112 Eric Weisstein's World of Mathematics, <a href="https://mathworld.wolfram.com/HeronianTriangle.html">Heronian Triangle</a>.
%H A391112 Eric Weisstein's World of Mathematics, <a href="https://mathworld.wolfram.com/PythagoreanTriangle.html">Pythagorean Triangle</a>.
%e A391112 a(24) = 2 because exactly 2 Heronian triangles with semiperimeter 24 can be decomposed into two Pythagorean triangles: (15, 15, 18) into twice (9, 12, 15), and (10, 17, 21) into (6, 8, 10) and (8, 15, 17).
%p A391112 A391112list := proc(N) # To get the terms a(1) to a(N).
%p A391112     local a, i, j, k, l, m, n;
%p A391112     a := Vector(N);
%p A391112     l := [];
%p A391112     for m from 2 to floor(sqrt(N - 1)) do
%p A391112         for k to m - 1 do
%p A391112             if igcd(m, k) = 1 and is(m - k, odd) then
%p A391112                 l := [op(l), seq(i*[m^2 - k^2, 2*m*k, m^2 + k^2], i = 1 .. N/(m^2 + k^2))];
%p A391112             fi
%p A391112         od
%p A391112     od;
%p A391112     for i to nops(l) do
%p A391112         for j to i do
%p A391112             if (l[i,1] = l[j,1] or l[i,1] = l[j,2]) then
%p A391112                 n := (l[i,2] + l[i,3] + l[j,1] + l[j,2] + l[j,3] - l[i,1])/2;
%p A391112                 if n <= N then
%p A391112                     a(n) := a(n) + 1
%p A391112                 fi
%p A391112             fi;
%p A391112             if (l[i,2] = l[j,1] or l[i,2] = l[j,2]) then
%p A391112                 n := (l[i,1] + l[i,3] + l[j,1] + l[j,2] + l[j,3] - l[i,2])/2;
%p A391112                 if n <= N then
%p A391112                     a(n) := a(n) + 1
%p A391112                 fi
%p A391112             fi
%p A391112         od
%p A391112     od;
%p A391112     return op(convert(a, list))
%p A391112 end proc:
%p A391112 A391112list(88);
%Y A391112 Cf. A009000, A078926, A334607.
%K A391112 nonn,new
%O A391112 1,18
%A A391112 _Felix Huber_, Dec 09 2025
